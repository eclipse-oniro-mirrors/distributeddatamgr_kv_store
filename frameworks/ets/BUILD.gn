# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/distributeddatamgr/kv_store/kv_store.gni")
copy_taihe_idl("kvstore_framework") {
  sources = [
    "idl/kvstore.taihe",
    "idl/kvstoreImport.taihe",
  ]
}
subsystem_name = "distributeddatamgr"
part_name = "kv_store"
taihe_generated_file_path =
    "${taihe_file_path}/out/${subsystem_name}/${part_name}"
ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path}"
  deps = [ ":kvstore_framework" ]
  outputs = [
    "${taihe_generated_file_path}/src/kvstore.abi.c",
    "${taihe_generated_file_path}/src/kvstore.ani.cpp",
  ]
}
taihe_shared_library("kvstore_framework_taihe_native") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "${taihe_generated_file_path}"
  part_name = "${part_name}"
  subsystem_name = "${subsystem_name}"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [
    "${kv_store_base_path}/frameworks/common",
    "${kv_store_base_path}/frameworks/jskitsimpl/distributeddata/include",
    "${kv_store_base_path}/frameworks/innerkitsimpl/distributeddatafwk/include",
    "${kv_store_base_path}/frameworks/innerkitsimpl/kvdb/include",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/kvstore.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "//foundation/distributeddatamgr/kv_store/interfaces/innerkits/distributeddata:distributeddata_inner",
  ]
  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_connect_callback_stub",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",
    "ability_runtime:ani_base_context",
    "ability_runtime:napi_base_context",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "hilog:libhilog",
    "ipc:ipc_single",
    "napi:ace_napi",
  ]
}
generate_static_abc("kvstore_framework_abc") {
  base_url = "${taihe_generated_file_path}"
  files = [ "${taihe_generated_file_path}/@ohos.data.distributedKVStore.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/kvstore_framework_abc.abc"
  dependencies = [ ":run_taihe" ]
}
ohos_prebuilt_etc("kvstore_framework_etc") {
  source = "${target_out_dir}/kvstore_framework_abc.abc"
  module_install_dir = "framework"
  part_name = "${part_name}"
  subsystem_name = "${subsystem_name}"
  deps = [ ":kvstore_framework_abc" ]
}
group("kvstore_taihe") {
  deps = [
    ":kvstore_framework_etc",
    ":kvstore_framework_taihe_native",
  ]
}
