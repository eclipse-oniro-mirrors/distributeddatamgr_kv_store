# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("distributeddb.gni")

config("distrdb_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "interfaces/include",
    "interfaces/include/cloud",
    "interfaces/src",
    "interfaces/src/relational",
    "common/include",
    "common/include/cloud",
    "common/include/relational",
    "common/src/cloud",
    "communicator/include",
    "storage/include",
    "storage/src",
    "storage/src/multiver",
    "storage/src/operation",
    "storage/src/gaussdb_rd",
    "storage/src/sqlite",
    "storage/src/sqlite/kv",
    "storage/src/sqlite/relational",
    "storage/src/upgrader",
    "storage/src/cloud",
    "storage/src/kv",
    "storage/src/relational",
    "syncer/include",
    "syncer/src",
    "syncer/src/cloud",
    "syncer/src/device",
    "syncer/src/device/multiver",
    "syncer/src/device/singlever",
  ]

  defines = [
    "_LARGEFILE64_SOURCE",
    "_FILE_OFFSET_BITS=64",
    "SQLITE_HAS_CODEC",
    "SQLITE_ENABLE_JSON1",
    "USING_HILOG_LOGGER",
    "USE_SQLITE_SYMBOLS",
    "USING_DB_JSON_EXTRACT_AUTOMATICALLY",
    "JSONCPP_USE_BUILDER",
    "OMIT_FLATBUFFER",
    "OMIT_MULTI_VER",
    "RELATIONAL_STORE",
    "SQLITE_DISTRIBUTE_RELATIONAL",
    "USE_DFX_ABILITY",
    "SQLITE_ENABLE_DROPTABLE_CALLBACK",
    "OPENSSL_SUPPRESS_DEPRECATED",
  ]
  if (is_debug) {
    defines += [ "TRACE_SQLITE_EXECUTE" ]
  }
  if (is_ohos) {
    defines += [ "USE_FFRT" ]
  }
  if (kv_store_cloud) {
    defines += [ "USE_DISTRIBUTEDDB_CLOUD" ]
  }
}

config("distrdb_public_config") {
  visibility = [ "*:*" ]
  include_dirs = [
    "interfaces/include",
    "interfaces/include/relational",
    "include",
    "gaussdb_rd/include",
    "gaussdb_rd/include/grd_base",
    "gaussdb_rd/include/grd_document",
    "gaussdb_rd/include/grd_kv",
  ]
}

group("build_module") {
  deps = [
    ":customtokenizer",
    ":distributeddb",
    ":distributeddbforrdbclient",
  ]
}

ohos_shared_library("distributeddb") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = distributeddb_src
  if (kv_store_cloud) {
    sources += distributeddb_cloud_src
  }

  configs = [ ":distrdb_config" ]
  public_configs = [ ":distrdb_public_config" ]

  cflags_cc = [
    "-fvisibility=hidden",
    "-Os",
    "-D_FORTIFY_SOURCE=2",
  ]
  deps = [ "gaussdb_rd:gaussdb_rd" ]

  external_deps = [
    "cJSON:cjson",
    "c_utils:utils",
    "ffrt:libffrt",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "zlib:shared_libz",
  ]

  public_external_deps = [
    "openssl:libcrypto_shared",
    "sqlite:sqlite",
  ]

  subsystem_name = "distributeddatamgr"
  innerapi_tags = [ "platformsdk_indirect" ]
  part_name = "kv_store"
}

tokenizer_path = "."

config("tokenizer_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$tokenizer_path/sqlite_adapter/include",
    "$tokenizer_path/sqlite_adapter/src",
  ]
  defines = [ "HARMONY_OS" ]
}

config("tokenizer_public_config") {
  visibility = [ ":*" ]
  include_dirs = [ "$tokenizer_path/sqlite_adapter/include" ]
}

ohos_shared_library("customtokenizer") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = [
    "$tokenizer_path/sqlite_adapter/src/tokenizer_api.cpp",
    "$tokenizer_path/sqlite_adapter/src/tokenizer_api_mgr.cpp",
    "$tokenizer_path/sqlite_adapter/src/tokenizer_sqlite.cpp",
  ]

  configs = [ ":tokenizer_config" ]
  public_configs = [ ":tokenizer_public_config" ]

  cflags_cc = [
    "-fvisibility=hidden",
    "-Os",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]

  public_external_deps = [ "sqlite:sqlite" ]

  subsystem_name = "distributeddatamgr"
  innerapi_tags = [ "platformsdk_indirect" ]
  part_name = "kv_store"
}

config("distrdbclient_public_config") {
  visibility = [ "*:*" ]
  include_dirs = [
    "interfaces/include/relational",
    "interfaces/include",
    "include",
  ]
}

config("distributeddbforrdbclient_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "interfaces/src/relational",
    "common/include/cloud",
    "common/include",
    "interfaces/include",
    "interfaces/include/cloud",
    "include",
    "storage/include",
    "communicator/include",
    "common/include/relational",
    "storage/src/sqlite",
    "storage/src",
    "storage/src/kv",
    "syncer/src/device",

    "storage/src/sqlite/relational",
    "storage/src/relational",
  ]

  defines = [
    "_LARGEFILE64_SOURCE",
    "_FILE_OFFSET_BITS=64",
    "SQLITE_HAS_CODEC",
    "SQLITE_ENABLE_JSON1",
    "USING_HILOG_LOGGER",
    "USE_SQLITE_SYMBOLS",
    "USING_DB_JSON_EXTRACT_AUTOMATICALLY",
    "JSONCPP_USE_BUILDER",
    "OMIT_FLATBUFFER",
    "OMIT_MULTI_VER",
    "RELATIONAL_STORE",
    "SQLITE_DISTRIBUTE_RELATIONAL",
    "USE_DFX_ABILITY",
    "SQLITE_ENABLE_DROPTABLE_CALLBACK",
    "OPENSSL_SUPPRESS_DEPRECATED",
  ]
  if (is_debug) {
    defines += [ "TRACE_SQLITE_EXECUTE" ]
  }
  if (is_ohos) {
    defines += [ "USE_FFRT" ]
    defines += [ "RDB_CLIENT" ]
  }
  if (kv_store_cloud) {
    defines += [ "USE_DISTRIBUTEDDB_CLOUD" ]
  }
}

ohos_shared_library("distributeddbforrdbclient") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = distributeddbforrdbclient_src
  configs = [ ":distributeddbforrdbclient_config" ]
  public_configs = [ ":distrdbclient_public_config" ]

  use_exceptions = true
  cflags_cc = [
    "-fvisibility=hidden",
    "-Os",
    "-D_FORTIFY_SOURCE=2",
  ]
  deps = [ "gaussdb_rd:gaussdb_rd" ]

  external_deps = [
    "c_utils:utils",
    "ffrt:libffrt",
    "hilog:libhilog",
  ]

  public_external_deps = [
    "openssl:libcrypto_shared",
    "jsoncpp:jsoncpp",
    "sqlite:sqlite",
  ]

  subsystem_name = "distributeddatamgr"
  innerapi_tags = [ "platformsdk_indirect" ]
  part_name = "kv_store"
}
